<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="MockDemo.UnitTests.CTestTestedClass">
<Super>Tests.Fw.CUnitTestBase</Super>
<TimeCreated>64377,70711.892343</TimeCreated>

<Property name="mathService">
<Type>MockDemo.MathService</Type>
<Private>1</Private>
</Property>

<Property name="testedClass">
<Type>MockDemo.CTestedClass</Type>
<Private>1</Private>
</Property>

<UDLText name="T">
<Content><![CDATA[
// --- Run test ---

]]></Content>
</UDLText>

<Method name="RunTests">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[   do ##super()
]]></Implementation>
</Method>

<Method name="OnBeforeOneTest">
<FormalSpec>testname:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
   set ..mathService = ..CreateMock()
   set ..testedClass = ##class(MockDemo.CTestedClass).%New(..mathService)
   quit $$$OK
]]></Implementation>
</Method>

<UDLText name="T">
<Content><![CDATA[
// --- Tests for SumOperation ---

]]></Content>
</UDLText>

<Method name="TestSumOperation">
<Implementation><![CDATA[
   do ..Expect(..mathService.Add(1, 2)).AndReturn(3).Times(3)
   do ..ReplayAllMocks()
   
   do $$$AssertEquals(..testedClass.SumOperation(1, 2), 3)
   do $$$AssertEquals(..testedClass.SumOperation(1, 2), 3)
   do $$$AssertEquals(..testedClass.SumOperation(1, 2), 3)
   
   do ..VerifyAllMocks()
]]></Implementation>
</Method>

<Method name="TestSumOperationParameterError">
<Description>
This tests fails only because it is intended to for demo purpose.</Description>
<Implementation><![CDATA[
   do ..Expect(..mathService.Add(1, 2)).AndReturn(3)
   do ..ReplayAllMocks()
   
   do $$$AssertEquals(..testedClass.SumOperation(1, 3), 3)
   
   do ..VerifyAllMocks()
]]></Implementation>
</Method>

<UDLText name="T">
<Content><![CDATA[
// --- Tests for ComplexOperation ---

]]></Content>
</UDLText>

<Method name="TestComplexOperationDivide">
<Description>
The mathService.Divide() method isn't even implemented. Yet this test is able
to verify that the status can be properly returned when passed by reference.
The result of 6/2 doesn't even count. The mock is told to return "50".</Description>
<Implementation><![CDATA[
   #Dim expectedStatus As %Status = $$$OK
   do ..Expect(..mathService.GreaterThan(6, 2)).AndReturn(1)
   do ..Expect(..mathService.Divide(6, 2, ..ByRefParam($$$OK, expectedStatus))).AndReturn(50)
   do ..ReplayAllMocks()
   
   #Dim status As %Status = $$$OK
   do $$$AssertEquals(..testedClass.ComplexOperation(6, 2, .status), 50)
   do $$$AssertStatusOK(status)
   
   do ..VerifyAllMocks()
]]></Implementation>
</Method>

<Method name="TestComplexOperationMultiply">
<Implementation><![CDATA[
   #Dim expectedStatus As %Status = $$$OK
   do ..Expect(..mathService.GreaterThan(6, 2)).AndReturn(0) // <-- Does not even have to be real.
   do ..Expect(..mathService.Multiply(6, 2, ..ByRefParam($$$OK, expectedStatus))).AndReturn(-25)
   do ..ReplayAllMocks()
   
   #Dim status As %Status = $$$OK
   do $$$AssertEquals(..testedClass.ComplexOperation(6, 2, .status), -25)
   do $$$AssertStatusOK(status)
   
   do ..VerifyAllMocks()
]]></Implementation>
</Method>

<Method name="TestComplexOperationErrorDivisionByZeroError">
<Implementation><![CDATA[
   #Dim expectedStatus As %Status = $$$ERROR("Divide by zero")
   do ..Expect(..mathService.GreaterThan(6, 0)).AndReturn(1)
   do ..Expect(..mathService.Divide(6, 0, ..ByRefParam($$$OK, expectedStatus))).AndReturn(-1)
   do ..ReplayAllMocks()
   
   #Dim status As %Status = $$$OK
   do $$$AssertEquals(..testedClass.ComplexOperation(6, 0, .status), -1)
   do $$$AssertStatusNotOK(status)
   
   do ..VerifyAllMocks()
]]></Implementation>
</Method>

<Method name="TestComplexOperationUnconsumedError">
<Description>
This tests fails on purpose</Description>
<Implementation><![CDATA[
   #Dim expectedStatus As %Status = $$$ERROR("Divide by zero")
   do ..Expect(..mathService.Divide(6, 2, ..ByRefParam($$$OK, expectedStatus))).AndReturn(50)
   do ..ReplayAllMocks()
   
   // NOTHING
      
   do ..VerifyAllMocks()
]]></Implementation>
</Method>

<Method name="TestComplexOperationUnexpectedCallError">
<Description>
This tests fails on purpose</Description>
<Implementation><![CDATA[
   // NO EXPECT

   do ..ReplayAllMocks()
   
   #Dim status As %Status = $$$OK
   do $$$AssertEquals(..testedClass.ComplexOperation(6, 2, .status), 50)
   do $$$AssertStatusNotOK(status)
   
   do ..VerifyAllMocks()
]]></Implementation>
</Method>

<UDLText name="T">
<Content><![CDATA[
// --- Tests for FetchSQLQueryInformation ---

]]></Content>
</UDLText>

<Method name="TestFetchSQLQueryInformation">
<Implementation><![CDATA[
   do ..SetMockResultSet("MockDemo.CTestedClass:MyQuery", 
                         ..%ClassName(1) _ "||ResultSetMockedDemo")
   do ..ReplayAllMocks()
   
   do $$$AssertEquals(..testedClass.FetchSQLQueryInformation("C"), "A+C")
   
   do ..VerifyAllMocks()
]]></Implementation>
</Method>

<XData name="ResultSetMockedDemo">
<Data><![CDATA[
<Query>
<ColumnsNames>,ResultA,ResultB</ColumnsNames>
<Row>A,C</Row>
<Row>A,D</Row>
</Query>
]]></Data>
</XData>

<UDLText name="T">
<Content><![CDATA[
// --- Tests for FetchSQLPrepareInformation ---

]]></Content>
</UDLText>

<Method name="TestFetchSQLPrepareInformation">
<Implementation><![CDATA[
   do ..SetMockResultSet("", 
                         ..%ClassName(1) _ "||ResultSetMockedDemo")
   do ..ReplayAllMocks()
   do $$$AssertEquals(..testedClass.FetchSQLPrepareInformation("C"), "|A+C|A+D")
   
   do ..VerifyAllMocks()
]]></Implementation>
</Method>

<Method name="TestFetchSQLPrepareInformationUnconsumedError">
<Description>
This test fails on purpose to show unconsumed SQL Queries</Description>
<Implementation><![CDATA[
   do ..SetMockResultSet("", 
                         ..%ClassName(1) _ "||ResultSetMockedDemo")
   do ..ReplayAllMocks()

   // NOTHING

   do ..VerifyAllMocks()
]]></Implementation>
</Method>

<Method name="TestPopulateObjectProperties">
<Description>
This tests validates Object passed in mocks</Description>
<Implementation><![CDATA[
   // Prepare expected test parameters
   set expectedHl7Message = ##class(EnsLib.HL7.Message).%New()
   set expectedHl7Message.DocType = "2.6:ADT_A01"
   do $$$AssertStatusOK(expectedHl7Message.SetValueAt("Content", "MSH:4"))
   
   set expectedStream = ##class(%Stream.TmpCharacter).%New()
   do expectedStream.Write("This is some CONtent")
   
   // Expect them
   do ..Expect(..mathService.GetResult(..ByRefParam($$$NULLOREF, expectedHl7Message),
                                       ..NotNullObject(##class(EnsLib.XML.SearchTable).%ClassName(1)),
                                       ..IsEqualObject(expectedStream))
                                       ).AndReturn($$$OK)
   do ..ReplayAllMocks()
   
   // Start the test
   #Dim hl7Message As EnsLib.HL7.Message = $$$NULLOREF
   set searchTable = ##class(EnsLib.XML.SearchTable).%New() 
   do $$$AssertStatusOK(..testedClass.PopulateObjectProperties(.hl7Message, searchTable))
   do $$$AssertEquals(hl7Message.DocType, "2.6:ADT_A01")
   do $$$AssertTrue(hl7Message.GetValueAt("MSH:4") = "Content")
   
   do ..VerifyAllMocks()
]]></Implementation>
</Method>
</Class>
</Export>
